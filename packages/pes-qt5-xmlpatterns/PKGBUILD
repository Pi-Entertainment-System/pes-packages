pkgname=pes-qt5-xmlpatterns
_qtver=5.14.2
pkgver=${_qtver/-/}
pkgrel=1
arch=('armv7h')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Support for XPath, XQuery, XSLT and XML schema validation'
depends=('pes-qt5-base' 'pes-qt5-declarative')
makedepends=('pes-qt5-base' 'pes-qt5-declarative')
conflicts=('qtchooser')
groups=('pes')
_pkgfqn="qtxmlpatterns-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz")
sha256sums=('219a876665345e3801baff71f31f30f5495c1cb9ab23fbbd27602632c80fcfb7')

prepare() {
  mkdir -p build
}

build() {
  cd build

  export PATH="/opt/pes/bin:$PATH"
  export LD_LIBRARY_PATH="/opt/pes/lib"

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build

  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/opt/pes/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/opt/pes/share/licenses
  ln -s /opt/pes/share/licenses/qt5-base "$pkgdir"/opt/pes/share/licenses/${pkgname}
}
