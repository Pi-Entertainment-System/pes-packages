pkgname=('pes-sixad')
pkgver=1.5.1.74
pkgrel=1
pkgdesc="PlayStation 3 SixAxis daemon and utilities based on qtsixa"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
groups=('pes')
license=('GPL')
depends=('bluez' 'bluez-libs' 'libusb-compat')
makedepends=('patch' 'bluez-libs')
conflicts=('sixpair' 'qtsixa')
replaces=('qtsixa')
source=(git+git://github.com/falkTX/qtsixa
	makefile.patch
	sixad.service
	uinput.patch
	97-sixpair.rules
)
md5sums=('SKIP' '0f27258594bca11e444627c53b295c04' 'be52d5b0f442f17ca3c236f7485b306f' '6205d115842120a629a94d88fc8f0822' 'a9816b7688f75935842d57db88e7dac8')

pkgver() {
  cd "$srcdir"/qtsixa

  version=$(grep "QtSixA version" qtsixa/gui/main.py | sed 's/  print "QtSixA version: //' | sed 's/"//')
  revision=$(git rev-list --count HEAD)

  echo $version.$revision
}

prepare() {
	cd "$srcdir"/qtsixa/sixad
	patch < "$srcdir"/makefile.patch
	patch < "$srcdir"/uinput.patch
	cd "$srcdir"/qtsixa
	sed -i 's:/sbin/:/opt/pes/sbin/:g' utils/Makefile
	sed -i 's:/usr/bin/:/opt/pes/bin/:g' sixad/Makefile
	sed -i 's:/usr/sbin/:/opt/pes/sbin/:g' sixad/Makefile
	#sed -i 's:/sbin/: /usr/sbin/:g' sixad/sixad
	#sed -i 's:/usr/sbin/: /usr/bin/:g' sixad/sixad
	sed -i 's:/usr/sbin/:/opt/pes/sbin/:g' sixad/bluetooth.cpp
}

build() {
	cd "$srcdir"/qtsixa/sixad
	make || exit 1
	cd "$srcdir"/qtsixa/utils
	gcc -lusb -o sixpair sixpair.c || exit 1
}

package() {
	install -d "$pkgdir"/usr/lib/systemd/system/
	install -m 644 "$srcdir"/sixad.service "$pkgdir"/usr/lib/systemd/system/
	install -d "$pkgdir"/etc/udev/rules.d
	install -m 644 "$srcdir"/97-sixpair.rules "$pkgdir"/etc/udev/rules.d/
	cd "$srcdir"/qtsixa/sixad
	install -d "$pkgdir"/etc/default/
	install -d "$pkgdir"/etc/logrotate.d/
	install -d "$pkgdir"/usr/bin/
	install -d "$pkgdir"/var/lib/sixad/
	install -d "$pkgdir"/var/lib/sixad/profiles/
	install -m 700 -d "$pkgdir"/opt/pes/sbin/
	install -m 700 bins/sixad-bin "$pkgdir"/opt/pes/sbin/
	install -m 700 bins/sixad-sixaxis "$pkgdir"/opt/pes/sbin/
	install -m 700 bins/sixad-remote "$pkgdir"/opt/pes/sbin/
	install -m 700 bins/sixad-3in1 "$pkgdir"/opt/pes/sbin/
	install -m 700 bins/sixad-raw "$pkgdir"/opt/pes/sbin/
	cd "$srcdir"/qtsixa/utils
	install -m 700 sixpair "$pkgdir"/opt/pes/sbin/
}
