pkgname=pes-qt5-quickcontrols
_qtver=5.14.2
pkgver=${_qtver/-/}
pkgrel=1
arch=('armv7h')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Reusable Qt Quick based UI controls to create classic desktop-style user interfaces'
depends=('pes-qt5-declarative')
makedepends=('pes-qt5-base')
groups=('pes')
_pkgfqn="qtquickcontrols-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz")
sha256sums=('d55def1dd4ee1250bd6a4e76849f4e362368b6411c2216d5f669c761216d4461')

prepare() {
  mkdir -p build
}

build() {
  cd build

  export PATH="/opt/pes/bin:$PATH"
  export LD_LIBRARY_PATH="/opt/pes/lib"

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/opt/pes/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/opt/pes/share/licenses
  ln -s /opt/pes/share/licenses/qt5-base "$pkgdir"/opt/pes/share/licenses/${pkgname}
}
