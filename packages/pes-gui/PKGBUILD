# Maintainer: Neil Munday <neil at mundayweb dot com>

pkgname=pes-gui
pkgver=3.0
pkgrel=1
pkgdesc="Pi Entertainment System (PES) GUI for gaming emulators"
groups=("pes")
arch=("any")
url="https://github.com/Pi-Entertainment-System/pes-gui"
license=("GPL")
depends=("python" "python-pyqt5" "python-pysdl2" "pes-rasum" "python-setuptools")
optdepends=("python-humanfriendly" "pes-libcec")
_commit='3e1a4ef1fc3d2f861b4e9d031fc2333275dfb417'
source=("git+https://github.com/Pi-Entertainment-System/pes-gui.git#commit=${_commit}")
sha256sums=('SKIP')

build() {
  cd "${srcdir}/pes-gui"
  if [ -e src/pes/data/pes.db ]; then
    echo "Using existing PES database"
  else
    echo "Creating PES database..."
    PYTHONPATH=src bin/populate-db.py -v -d src/pes/data -m -r -g --match
  fi
}

package() {
  cd "${srcdir}/pes-gui"
  python setup.py install --root="${pkgdir}" --prefix="/opt/pes"
}
