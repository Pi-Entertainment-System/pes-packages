pkgname=pes-sdl2
_sdlver=2.0.12
pkgver=${_sdlver}
pkgrel=1
arch=('armv7h')
url='https://www.libsdl.org'
license=('zlib')
pkgdesc='SDL2 library'
depends=('alsa-lib')
makedepends=('make' 'alsa-lib')
groups=('pes')
source=("https://www.libsdl.org/release/SDL2-${_sdlver}.tar.gz")
sha256sums=('349268f695c02efbc9b9148a70b85e58cefbbf704abd3e91be654db7f1e2c863')

prepare() {
  mkdir -p build
}

build() {
  cd build

  export CFLAGS="-mcpu=cortex-a7 -mfpu=neon-vfpv4 -mfloat-abi=hard"
  export CXXFLAGS=$CFLAGS

  ../SDL2-${_sdlver}/configure --prefix=/opt/pes \
	--host=arm-raspberry-linux-gnueabihf \
	--disable-video-opengl \
	--disable-video-x11 \
	--disable-pulseaudio \
	--disable-esd \
	--enable-video-opengles \
	--enable-libudev \
	--enable-alsa

  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
