pkgname=pes-qt5-quickcontrols2
_qtver=5.14.2
pkgver=${_qtver/-/}
pkgrel=1
arch=('armv7h')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Next generation user interface controls based on Qt Quick'
depends=('pes-qt5-declarative')
groups=('pes')
_pkgfqn="qtquickcontrols2-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz")
sha256sums=('faf7d349d8f4a8db36cd3c62a5724bcf689300f2fdb7dc1ea034392aab981560')

prepare() {
  mkdir -p build
}

build() {
  cd build

  export PATH=/opt/pes/bin:$PATH
  export LD_LIBRARY_PATH=/opt/pes/lib

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/opt/pes/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/opt/pes/share/licenses
  ln -s /opt/pes/share/licenses/qt5-base "$pkgdir"/opt/pes/share/licenses/${pkgname}
}
