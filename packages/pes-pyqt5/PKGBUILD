pkgname=pes-pyqt5
pkgver=5.14.2
pkgrel=1
arch=('armv7h')
url="https://riverbankcomputing.com/software/pyqt/intro"
license=('GPL')
pkgdesc="A set of Python bindings for the Qt5 toolkit"
makedepends=('make' 'sip5' 'python-pyqt5-sip' 'python-dbus' 'pes-qt5-base' 'pes-qt5-declarative' 'pes-qt5-xmlpatterns'
'pes-qt5-quickcontrols' 'pes-qt5-quickcontrols2')
depends=('python-pyqt5-sip' 'python-dbus' 'pes-qt5-base' 'pes-qt5-declarative' 'pes-qt5-xmlpatterns'
'pes-qt5-quickcontrols' 'pes-qt5-quickcontrols2')
groups=(pes)
source=("https://pypi.python.org/packages/source/P/PyQt5/PyQt5-$pkgver.tar.gz" 'config')
sha256sums=('bd230c6fd699eabf1ceb51e13a8b79b74c00a80272c622427b80141a22269eb0' '6bd5f1102ee3fc95c7d0231e8884f623437aa8c209a8d10c64d73084a1964329')

prepare() {
  cp ${srcdir}/config ${srcdir}/PyQt5-${pkgver}
}

build() {
  export PATH=/opt/pes/bin:$PATH
  export LD_LIBRARY_PATH=/opt/pes/lib

  cd ${srcdir}/PyQt5-${pkgver}
  python ./configure.py -w --no-sip-files \
    --confirm-license \
    --qsci-api \
    --destdir=/opt/pes/lib/python3.8/site-packages \
    --stubsdir=/opt/pes/lib/python3.8/site-packages/PyQt5 \
    --sipdir=/opt/pes/share/sip/PyQt5 \
    --bindir=/opt/pes/bin \
    --qmake=/opt/pes/bin/qmake \
    --disable=QtPrintSupport \
    --configuration=config
  make
}

package(){
  install -d "$pkgdir"/opt/pes/bin
  install -d "$pkgdir"/opt/pes/lib
  install -d "$pkgdir"/opt/pes/share/sip/PyQT5

  cd PyQt5-$pkgver
  make DESTDIR="$pkgdir" INSTALL_ROOT="$pkgdir" install -j1

  # Remove unused py2 version of uic modules:
  rm -r "$pkgdir"/opt/pes/lib/python3.8/site-packages/PyQt5/uic/port_v2

  # compile Python bytecode
  python -m compileall -d / "$pkgdir"/opt/pes/lib
  python -O -m compileall -d / "$pkgdir"/opt/pes/lib

  # move dbus module
  mv "$pkgdir"/usr/lib/python3.8/site-packages/dbus "$pkgdir"/opt/pes/lib/python3.8/site-packages
  rm -rf "$pkgdir"/usr
}
